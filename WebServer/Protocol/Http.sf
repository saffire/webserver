import Response from Http;
import ResponseCode from Http;
import Request from Http;
import Kernel from Http;
import Processor from \WebServer;
import io;
import datetime;

class Http implements Processor {
    protected property connection;
    protected property config;

    public method __ctor(hash config)
    {
        self.config = config;
    }

    public method handle(Connection connection)
    {
        self.connection = connection;

        request = self.readRequest();
        if (! request) {
            io.println("No request.. (response already sent)");
            return;
        }

        kernel = Kernel();
        kernel.setConfiguration(self.config);
        response = kernel.process(request);

        if (! response) {
            io.println("No repsonse given, internal server error");
            response = Response(ResponseCode.STATUS_INTERNAL_SERVER_ERROR);
        }

        self.returnResponse(response);
    }

    public method readRequest()
    {
        // Check initial header line
        initial = self.connection.readLine();

        (ret, matches) = regex.match('|^(?P<method>\w+)\s+(.+)\s+HTTP\/(?P<version>\d.\d)$|', initial);
        if (! ret) {
            response = Response(ResponseCode.STATUS_BAD_REQUEST);
            self.returnResponse(response);

            return null;
        }

        httpMethod = matches['method'];
        httpUrl = matches[2];
        httpVersion = matches['version'];

        // Check HTTP version
        if (httpVersion != "1.0" && httpVersion != "1.1") {
            response = Response(ResponseCode.STATUS_VERSION_NOT_SUPPORTED, "", hash[[]], "");
            self.returnResponse(response);

            return null;
        }

        // Read additional headers
        headers = hash[[]];
        s = "1";
        while (s) {
            s = self.connection.readLine();
            if (! s) continue;

//            (k, v) = s.split(":", 2);
//            headers[k] = v.trim();
        }

        body = "";

        now = datetime.now();
        info = hash[[
            'request_time' : now.format(datetime.RFC1123),
            'request_time_epoch' : now.epoch(),
            'document_root' : self.config['doc_root'],
            'remote_address' : self.connection.getHost(),
            'remote_port' : self.connection.getPort(),
        ]];

        request = Request(httpMethod, httpUrl, headers, body, info);

        return request;
    }

    protected method returnResponse(Response response)
    {
        self.setupResponseHeaders(response);
        self.sendResponse(response);
    }

    protected method setupResponseHeaders(Response response)
    {
        // Set default status message is none is set
        if (response.getStatusText() == "") {
            response.setStatusText(ResponseCode.getStatusText(response.getStatusCode()));
        }

        // Add additional headers
        len = response.getBody().length();
        if (len != 0) {
            response.headers['content-length'] = response.getBody().length();
        }
        response.headers['connection'] = 'close';
        response.headers['date'] = datetime.now().format(datetime.RFC1123);
        response.headers['server'] = 'Saffire WebServer/1.0';
    }

    protected method sendResponse(Response response)
    {
        io.println("> Response status[", response.getStatusCode(),"] Body[", response.getBody().length(),"]");

        // Send initial header line
        self.connection.write("HTTP/1.1 " + response.getStatusCode().__string() + " " + response.getStatusText().upper() +0 "\r\n");

        // Send headers
        foreach (response.headers as k,v) {
            self.connection.write(k.lower().ucfirst() + ": " + (v.__string() ?? "") + "\r\n");
        }

        // Header/Body separator
        self.connection.write("\r\n");

        // Send body
        self.connection.write(response.getBody());
    }

}
